buildscript {
    ext {
        adapterVersion = '1.0.4'
        // Android SDK versions
        compileSdkVersion = 33
        targetSdkVersion = 33
        minSdkVersion = 21
    }
}

plugins {
    alias(libs.plugins.benmanes.versions)
    alias(libs.plugins.nexus.publishing)

    alias(libs.plugins.kotlin.android).apply(false)
    alias(libs.plugins.android.library).apply(false)
}

task getVersion() {
    doLast {
        println adapterVersion
    }
}

nexusPublishing {
    // user and pass are required for publishing.
    def user = System.getenv("SONATYPE_USERNAME") ?: findProperty("sonatype.username")
    def pass = System.getenv("SONATYPE_PASSWORD") ?: findProperty("sonatype.password")
    // profileId is optional, but supposedly speeds things up if set.
    def profileId = System.getenv("SONATYPE_STAGING_PROFILE_ID") ?: findProperty("sonatype.stagingProfileId")
    if (user != null && pass != null) {
        repositories {
            sonatype {
                username.set(user)
                password.set(pass)
                stagingProfileId.set(profileId)
            }
        }
        transitionCheckOptions {
            maxRetries.set(100)
            delayBetween.set(Duration.ofSeconds(60*2))
        }
        connectTimeout = Duration.ofSeconds(60*30)
        clientTimeout = Duration.ofSeconds(60*30)
    } else {
        logger.info("Missing publishing credentials! Nexus publishing will not be configured.")
    }
}
